(robin (0 . 1) (small (0 . 1))
  (let (
    (empty? (fun (li)
      (equal? li ())))
    (list (macro (self args env)
      (if (empty? args)
        ()
        (pair (eval env (head args))
              (eval env (pair self (tail args)))))))
    (list? (fun (li)
      (bind list?-r (fun (self li)
        (if (empty? li)
          #t
          (if (pair? li)
            (self self (tail li))
            #f)))
        (list?-r list?-r li))))
    (map (fun (app li)
      (bind map-r
        (fun (self app li)
          (if (empty? li)
            ()
            (pair (app (head li)) (self self app (tail li)))))
        (map-r map-r app li))))
    (fold (fun (app acc li)
      (bind fold-r (fun (self app acc li)
        (if (empty? li)
          acc
          (self self app (app (head li) acc) (tail li))))
        (fold-r fold-r app acc li))))
    (reverse (fun (li)
      (fold pair () li)))
    (filter (fun (pred li)
      (reverse (fold
        (fun (x acc) (if (pred x) (pair x acc) acc))
        () li))))
    (find (fun (pred li)
      (bind find-r (fun (self pred li)
        (if (empty? li)
          ()
          (if (pred (head li))
            (pair (head li) ())
            (self self pred (tail li)))))
        (find-r find-r pred li))))
    (append (fun (li new-tail)
      (bind append-r (fun (self li new-tail)
        (if (empty? li)
          new-tail
          (pair (head li) (self self (tail li) new-tail))))
      (append-r append-r li new-tail))))
    (elem? (fun (item li)
      (bind elem?-r (fun (self item li)
        (if (empty? li)
          #f
          (if (equal? item (head li))
            #t
            (self self item (tail li)))))
      (elem?-r elem?-r item li))))
    (length (fun (li)
      (subtract 0 (fold (fun (x acc) (subtract acc 1)) 0 li))))
    (index (fun (index li)
      (bind index-r (fun (self index li)
        (if (equal? index 0)
          (head li)
          (self self (subtract index 1) (tail li))))
      (index-r index-r index li))))
    (take-while (fun (pred li)
      (bind take-while-r (fun (self pred li)
        (if (empty? li)
          ()
          (if (pred (head li))
            (pair (head li) (self self pred (tail li)))
            ())))
        (take-while-r take-while-r pred li))))
    (drop-while (fun (pred li)
      (bind drop-while-r (fun (self pred li)
        (if (empty? li)
          ()
          (if (pred (head li))
            (self self pred (tail li))
            li)))
        (drop-while-r drop-while-r pred li))))
    (first (fun (n li)
      (bind first-r (fun (self n li)
        (if (equal? n 0)
          ()
          (pair (head li) (self self (subtract n 1) (tail li)))))
        (first-r first-r n li))))
    (rest (fun (n li)
      (bind rest-r (fun (self n li)
        (if (equal? n 0)
          li
          (self self (subtract n 1) (tail li))))
        (rest-r rest-r n li))))
    (last (fun (n li)
      (reverse (first n (reverse li)))))
    (prefix? (fun (la lb)
      (bind prefix?-r (fun (self la lb)
        (if (empty? la)
          #t
          (if (empty? lb)
            #f
            (if (equal? (head la) (head lb))
              (self self (tail la) (tail lb))
              #f))))
        (prefix?-r prefix?-r la lb))))
    (flatten (fun (li)
      (bind flatten-r (fun (self li acc)
        (if (empty? li)
          acc
          (if (list? (head li))
            (self self (tail li) (self self (head li) acc))
            (self self (tail li) (pair (head li) acc)))))
        (reverse (flatten-r flatten-r li ())))))
    )
    (list
      (pair (literal list) list)
      (pair (literal empty?) empty?)
      (pair (literal list?) list?)
      (pair (literal map) map)
      (pair (literal fold) fold)
      (pair (literal reverse) reverse)
      (pair (literal filter) filter)
      (pair (literal find) find)
      (pair (literal append) append)
      (pair (literal elem?) elem?)
      (pair (literal length) length)
      (pair (literal index) index)
      (pair (literal take-while) take-while)
      (pair (literal drop-while) drop-while)
      (pair (literal first) first)
      (pair (literal rest) rest)
      (pair (literal last) last)
      (pair (literal prefix?) prefix?)
      (pair (literal flatten) flatten)
    )
  )
)
